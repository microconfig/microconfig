apply from: 'lib.gradle'
apply from: 'pomConfig.gradle'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.8'
    }
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'org.sonarqube'

//    apply plugin: 'signing' //todo

    group = 'io.microconfig'
    version = rootProject.version

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            nexus(MavenPublication) {
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', pomDescription)
                    root.children().last() + pomConfig
                }
                from components.java
            }
        }
        repositories {
            maven {
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

//    signing {
//        sign publishing.publications.nexus
//    }

    dependencies {
        implementation platform(lib.spring_bom)

        compileOnly lib.lombok
        testCompileOnly lib.lombok
        annotationProcessor lib.lombok

        testImplementation lib.junit, lib.mokito
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "microconfig_microconfig"
            property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
            property "sonar.sources", "src"
            property "sonar.binaries", "build/classes"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }


    test {
        useJUnitPlatform()
        exclude '**/*IT.class'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }
}